pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('x86_64')
license=('MIT')

url=https://st.suckless.org/
source=("https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz"
	"https://st.suckless.org/patches/desktopentry/st-desktopentry-0.8.2.diff"
	"https://st.suckless.org/patches/newterm/st-newterm-0.8.2.diff"
	"https://st.suckless.org/patches/scrollback/st-scrollback-20200419-72e3f6c.diff"
	)

sha256sums=('d42d3ceceb4d6a65e32e90a5336e3d446db612c3fbd9ebc1780bc6c9a03346a6'
            '5ddfc3954698b43609cbe19d2beb397dc2bb897998122f451c1d84886217817e'
	    '21652c7b964e2958577ae66deeb94d0c07f69b22c3a9ad9b8bfb83eadb622783'
	    '1e41fe17a5ef5a8194eea07422b49d815e2c2bb4d58d84771f793be423005310'
)

extractdir="$pkgname-$pkgver"

prepare() {
	patch --directory="$extractdir" --strip=1 -i "$srcdir/st-desktopentry-0.8.2.diff"
	patch --directory="$extractdir" --strip=1 -i "$srcdir/st-newterm-0.8.2.diff"
	patch --directory="$extractdir" --strip=1 -i "$srcdir/st-scrollback-20200419-72e3f6c.diff"

	cp "$srcdir/$extractdir/config.def.h" "$BUILDDIR"

	if [ -f "$BUILDDIR/config.h" ]
	then
		cp "$BUILDDIR/config.h" "$srcdir/$extractdir"
	fi
}

build() {
	make --directory="$extractdir" X11INC="/usr/include/X11/" X11LIB="/usr/lib/X11/"
}

package() {
	make --directory="$extractdir" PREFIX=/usr DESTDIR="$pkgdir/" install
}
